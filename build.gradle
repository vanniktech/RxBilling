apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    kotlin: '1.3.11',
    autoValue: '1.6.3',
    espresso: '3.1.0',
    testRules: '1.1.0',
  ]

  ext.deps = [
    'rx': [
      'java': 'io.reactivex.rxjava2:rxjava:2.2.4',
    ],
    'autoValue': [
      'compiler': "com.google.auto.value:auto-value:${versions.autoValue}",
      'annotations': "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
    ],
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
    ],
    'support': [
      'annotations': 'androidx.annotation:annotation:1.0.0',

      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
        ],

        'runner': "androidx.test:runner:${versions.testRules}",
        'junitExt': "androidx.test.ext:junit:1.0.0",
        'rules': "androidx.test:rules:${versions.testRules}",
      ],
    ],
    'test': [
      'mockito': 'org.mockito:mockito-core:2.23.0',
      'junit': 'junit:junit:4.12',
      'assertj3': 'org.assertj:assertj-core:3.11.1',
      'assertj2': 'org.assertj:assertj-core:2.9.0',
    ],
    'navi': "com.trello.navi2:navi:2.1.0",
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.android.tools.build:gradle:3.2.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'

    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.12.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.6.0'
  }
}

codeQualityTools {
  lint {
    textReport = true
  }
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  errorProne {
    toolVersion = '2.1.2'
  }
  ktlint {
    toolVersion = '0.28.0'
  }
  detekt {
    toolVersion = '1.0.0-RC12'
  }
  cpd {
    enabled = false
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  project.plugins.withId("com.android.library") {
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    project.android.libraryVariants.all {
      it.generateBuildConfig.enabled = false
    }
  }
}

wrapper {
  gradleVersion = '5.0'
  distributionType = Wrapper.DistributionType.ALL
}
