apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    kotlin: '1.4.31',
    autoValue: '1.7.4',
    espresso: '3.1.1',
    testRules: '1.1.1',
  ]

  ext.deps = [
    'rx': [
      'java': 'io.reactivex.rxjava2:rxjava:2.2.21',
    ],
    'autoValue': [
      'compiler': "com.google.auto.value:auto-value:${versions.autoValue}",
      'annotations': "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
    ],
    'support': [
      'annotations': 'androidx.annotation:annotation:1.0.1',

      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
        ],

        'runner': "androidx.test:runner:${versions.testRules}",
        'junitExt': "androidx.test.ext:junit:1.1.0",
        'rules': "androidx.test:rules:${versions.testRules}",
      ],
    ],
    'test': [
      'mockito': 'org.mockito:mockito-core:2.23.0',
      'junit': 'junit:junit:4.13.2',
      'assertj': 'org.assertj:assertj-core:2.9.1',
    ],
    'billing': "com.android.billingclient:billing:1.2.2",
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.android.tools.build:gradle:4.1.2'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'

    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
  }
}

codeQualityTools {
  lint {
    textReport = true
  }
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  errorProne {
    toolVersion = '2.1.2'
  }
  ktlint {
    toolVersion = '0.31.0'
  }
  detekt {
    toolVersion = '1.0.0-RC14'
  }
  cpd {
    enabled = false
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  project.plugins.withId("com.android.library") {
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    project.android.libraryVariants.all {
      it.generateBuildConfig.enabled = false
    }
  }
}

wrapper {
  gradleVersion = '6.7.1'
  distributionType = Wrapper.DistributionType.ALL
}
